plugins {
    id 'kotlin-multiplatform' version '1.3.11'
}
repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/spekframework/spek-dev"
    }
}
group 'com.example'
version '0.0.1'

apply plugin: 'maven-publish'

def Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())
def nexusUrl = properties.getProperty("nexusUrl")
def nexusUsername = properties.getProperty("nexusUsername")
def nexusPassword = properties.getProperty("nexusPassword")

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            credentials {
                username nexusUsername
                password nexusPassword
            }
            def releasesRepoUrl = "$nexusUrl/repository/maven-releases"
            def snapshotsRepoUrl = "$nexusUrl/repository/maven-snapshots"
            url = uri((version.toString().endsWith("SNAPSHOT"))? snapshotsRepoUrl: releasesRepoUrl)
        }
    }
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        // This preset is for iPhone emulator
        // Switch here to presets.iosArm64 to build library for iPhone device
        fromPreset(presets.iosX64, 'ios') {
            compilations.main.outputKinds('FRAMEWORK')
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        iosMain {
        }
        iosTest {
        }
    }
}



configurations {
    compileClasspath
}